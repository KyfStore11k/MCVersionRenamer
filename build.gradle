plugins {
	id 'java'
	id 'checkstyle'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.gradleup.shadow' version '9.0.0-beta6'
}

version(project.version)
group(project.maven_group)

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.slf4j:slf4j-api:$project.slf4j_version"
	implementation "ch.qos.logback:logback-classic:$project.logback_version"

	implementation "org.json:json:$project.orgjson_version"
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

processResources {
	filesMatching(['**/*.json']) {
		expand([version: version])
	}
}

checkstyle {
	toolVersion = '10.21.4'
	config = resources.text.fromFile('config/checkstyle/checkstyle.xml')
	maxErrors = 0
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21

	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.jar.enabled = false
tasks.build.dependsOn(shadowJar)

shadowJar {
	archiveBaseName.set("MCVersionRenamerInstaller")
	archiveClassifier.set("")
	archiveVersion.set("")

	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}

	manifest {
		attributes(
				"Main-Class": "com.kyfstore.mcversionrenamer.application.Window"
		)
	}
}